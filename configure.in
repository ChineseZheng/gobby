#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT([gobby], [0.1.0], [crew@0x539.de])
AM_INIT_AUTOMAKE
AC_CONFIG_SRCDIR([inc/window.hpp])
AC_CONFIG_HEADER([inc/features.hpp])

# Extract host information.
AC_CANONICAL_HOST

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_CACHE_SAVE

AC_MSG_CHECKING([whether to enable WIN32 specific flags])
if test "x$host_os" = "xmingw32" ; then
        win32=true
        AC_MSG_RESULT([yes])
else
        win32=false
        AC_MSG_RESULT([no])
fi
AM_CONDITIONAL(WIN32, test x$win32 = xtrue)

# Let the user choose if he wants to use gtksourceview or not. The source is
# currently under development and does not work (default to no).
AC_ARG_WITH([gtksourceview],
	    AS_HELP_STRING([--with-gtksourceview],
			   [use SourceView instead of TextView [[default=no]]]),
	    [ac_cv_use_gtksourceview=$withval], [ac_cv_use_gtksourceview=no])
AC_CACHE_CHECK([whether to use GtkSourceView], [ac_cv_use_gtksourceview],
	       [ac_cv_use_gtksourceview=no])
AM_CONDITIONAL(WITH_GTKSOURCEVIEW, test x$ac_cv_use_gtksourceview != xno)

# Checks for libraries.
PKG_CHECK_MODULES([gobby], [libxml++-2.6 gtkmm-2.4 >= 2.6.0 obby-1.0])
if test "x$ac_cv_use_gtksourceview" != "xno" ; then
	PKG_CHECK_MODULES([gtksourceview], [gtksourceview-1.0])
	AC_DEFINE([WITH_GTKSOURCEVIEW], 1,
		  [Define whether to use SourceView instead of TextView.])
fi

# Check for howl in libobby.
AC_CHECK_LIB(obby, obby_has_howl)
if test "x$ac_cv_lib_obby_obby_has_howl" = "xyes" ; then
	# We do not need any new flags as obby should take care of it.
	PKG_CHECK_MODULES([howl], [howl])
	AC_DEFINE([WITH_HOWL], 1, [Define whether to use howl support])
fi
AM_CONDITIONAL(WITH_HOWL, test x$ac_cv_lib_obby_obby_has_howl = xyes)

# Glib i18n/gettext support.
ALL_LINGUAS="de"
AM_GLIB_GNU_GETTEXT

GETTEXT_PACKAGE=$PACKAGE
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE], ["${GETTEXT_PACKAGE}"], [gettext domain])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT([po/Makefile.in])

